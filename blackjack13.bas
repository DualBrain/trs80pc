  100 "B" PRINT "BLACKJACK 13": IF R=0 INPUT "TIME (HHMM):";R:R=R/2400
  110 "N" PAUSE "CLEARING TABLE":O=26: FOR Z=1 TO 14:A(Z)=0: IF Z<10 LET A(O+Z)=Z
  120 NEXT Z:P$="PLAYER ":Q$="DEALER ":S=0:T=0:X$=""
  130 PAUSE "SHUFFLING": FOR Z=27 TO 35:R=Π+997R:R=R-INT R:T=INT 9R+27:U=A(T):A(T)=A(Z):A(Z)=U: NEXT Z
  140 "H" IF S=0 IF M<13 LET O=O+1:K=K+1:A(K)=A(O):M=M+A(O): IF M>13 GOTO "D"
  150 "E" IF N<10 LET O=O+1:L=L+1:A(L+5)=A(O):N=N+A(O): IF S GOTO "E"
  160 "D" PRINT "P:";A;B;C;D;E;" D:";F;G;H;I;J
  170 T=M>13:U=N>13: IF T+U LET V=17-T: PRINT A$(V);"BUSTS!": GOTO "P"
  180 IF S LET T=M*(T=0):U=N*(U=0):V=16+(U>T): PRINT A$(V);"WINS.": GOTO "P"
  190 INPUT "(H)IT OR (S)TAY? ";X$: IF X$="H" GOTO X$
  200 IF X$="S" GOTO X$
  205 GOTO "D"
  210 "S"S=1: GOTO "E"
  220 "P" INPUT "PLAY AGAIN (Y/N)? ";X$: IF X$="Y" GOTO "N"
  230 "X" PRINT "GOOD BYE.": END 
  300 "=" PRINT A(27);A(28);A(29);A(30);A(31);A(32);A(33);A(34);A(35);A(36): END 
  900 "R" IF R=0 INPUT "TIME (HHMM):";R:R=1/R
  905 R=Π+997R:R=R-INT R: RETURN 
  990 "Z" END 
